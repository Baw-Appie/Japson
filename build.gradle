import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'com.github.johnrengelman.shadow' version '5.2.0'
	id 'maven-publish'
	id 'eclipse'
	id 'java'
}

jar.archiveName = project.name + '.jar'
version '1.0.4'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

	// Google Flogger
	shadow (group: 'com.google.flogger', name: 'flogger-system-backend', version: '0.5.1')
	shadow (group: 'com.google.flogger', name: 'flogger', version: '0.5.1')

	// Google Gson
	shadow (group: 'com.google.code.gson', name: 'gson', version: '2.8.6')

	// Google Guava
	shadow (group: 'com.google.guava', name: 'guava', version: '29.0-jre')

}

publishing {
	repositories {
		maven {
			name = "Japson"
			url = uri("https://maven.pkg.github.com/Sitrica/Japson")
			credentials {
				username = 'Sitrica'
				password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGES_KEY")
			}
		}
	}
	publications {
		shadow(MavenPublication) {publication ->
			project.shadow.component(publication)
			version = version
			groupId = 'com.sitrica'
			artifactId = 'japson' // Don't modify
		}
	}
}

processResources {
	filter ReplaceTokens, tokens: ["version": version]
	from (sourceSets.main.resources.srcDirs) {
		include '*.yml'
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveVersion = version
	baseName = project.name
	classifier = ''
	minimize {
		exclude(dependency('com.google.flogger:'))
	}
	dependencies {
		exclude(dependency('org.spigotmc:'))
		exclude(dependency('org.yaml:'))
		exclude(dependency('io.netty:'))
	}
}
